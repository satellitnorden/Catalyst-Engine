//Version declaration.
#version 460

//Extensions.
#extension GL_GOOGLE_include_directive : enable

//Includes.
#include "CatalystShaderCommon.glsl"
#include "CatalystLightingData.glsl"
#include "CatalystPackingUtilities.glsl"
#include "CatalystRayTracingCore.glsl"
#include "CatalystShaderPhysicallyBasedLighting.glsl"

//Descriptor set data.
layout (set = 1, binding = 0, rgba32f) uniform image2D diffuseIrradianceTexture;
layout (set = 1, binding = 1, rgba32f) uniform image2D specularIrradianceTexture;
layout (set = 1, binding = 2, rgba8) uniform image2D sceneFeatures1Texture;
layout (set = 1, binding = 3, rgba32f) uniform image2D sceneFeatures2Texture;
layout (set = 1, binding = 4, rgba8) uniform image2D sceneFeatures3Texture;
layout (set = 1, binding = 5, rgba32f) uniform image2D sceneTexture;

void main()
{
	//Sample the normal and hit distance.
	vec4 sceneFeatures2TextureSampler = imageLoad(sceneFeatures2Texture, ivec2(gl_GlobalInvocationID.xy));
	vec3 normal = UnpackNormal(sceneFeatures2TextureSampler.y);
	float hitDistance = sceneFeatures2TextureSampler.w;

	//Only calculate the lighting if there was a hit.
	if (hitDistance < CATALYST_RAY_TRACING_T_MAXIMUM)
	{
		//Sample the diffuse irradiance.
		vec3 diffuseIrradiance = imageLoad(diffuseIrradianceTexture, ivec2(gl_GlobalInvocationID.xy)).rgb;

		//Sample the specular irradiance.
		vec3 specularIrradiance = imageLoad(specularIrradianceTexture, ivec2(gl_GlobalInvocationID.xy)).rgb;

		//Sample the albedo.
		vec3 albedo = imageLoad(sceneFeatures1Texture, ivec2(gl_GlobalInvocationID.xy)).rgb;

		//Sample the roughness and metallic.
		vec4 sceneFeatures3TextureSampler = imageLoad(sceneFeatures3Texture, ivec2(gl_GlobalInvocationID.xy));
		float roughness = sceneFeatures3TextureSampler.x;
		float metallic = sceneFeatures3TextureSampler.y;
		float ambientOcclusion = sceneFeatures3TextureSampler.z;

		//Calculate the world position.
		vec3 worldPosition = perceiverWorldPosition + CalculateRayDirection((vec2(gl_GlobalInvocationID.xy) + vec2(0.5f)) / vec2(1920.0f, 1080.0f)) * hitDistance;

		//Calculate the view direction.
		vec3 viewDirection = normalize(perceiverWorldPosition - worldPosition);

		//Calculate the composited lighting.
		vec3 compositedLighting = vec3(0.0f);

		//Add the indirect lighting.
		compositedLighting += CalculateIndirectLighting(viewDirection,
														albedo,
														normal,
														roughness,
														metallic,
														ambientOcclusion,
														diffuseIrradiance,
														specularIrradiance);

		//Add the directional light direct lighting result.
		float directionalLightVisibility = imageLoad(directionalLightVisibilityTexture, ivec2(gl_GlobalInvocationID.xy)).x;

		compositedLighting += CalculateDirectLight(	viewDirection,
													-directionalLightDirection,
													albedo,
													normal,
													roughness,
													metallic,
													directionalLightColor * directionalLightIntensity) * directionalLightVisibility;

		//Add the lights direct lighting results.
		for (int i = 0; i < numberOfLights; ++i)
		{
			Light light = UnpackLight(i);

			float lengthToLight = length(light.position - worldPosition);
			vec3 lightDirection = vec3(light.position - worldPosition) / lengthToLight;

			//Calculate the attenuation distance.
			float attenuation = 1.0f / (1.0f + lengthToLight + (lengthToLight * lengthToLight));

			//Unpack the visibility.
			float lightVisibility = imageLoad(lightsVisibilityTextures[i], ivec2(gl_GlobalInvocationID.xy)).x;

			//Calculate the lighting.
			compositedLighting += CalculateDirectLight(	viewDirection,
														lightDirection,
														albedo,
														normal,
														roughness,
														metallic,
														light.color * light.strength) * attenuation * lightVisibility;
		}
		
		//Write the fragment.
		imageStore(sceneTexture, ivec2(gl_GlobalInvocationID.xy), vec4(compositedLighting, 1.0f));
	}
	
	//If there wasn't a hit, then the sky color is stored in the specular irradiance texture.
	else
	{
		//Write the fragment.
		imageStore(sceneTexture, ivec2(gl_GlobalInvocationID.xy), vec4(imageLoad(specularIrradianceTexture, ivec2(gl_GlobalInvocationID.xy)).rgb, 1.0f));
	}
}