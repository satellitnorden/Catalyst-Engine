//Version declaration.
#version 460

//Extensions.
#extension GL_GOOGLE_include_directive : enable

//Includes.
#include "CatalystShaderCommon.glsl"
#include "CatalystRayTracingCore.glsl"
#include "CatalystToneMappingUtilities.glsl"

//Descriptor set data.
layout (set = 1, binding = 0, rgba32f) uniform image2D indirectLightingImage;
layout (set = 1, binding = 1, rgba32f) uniform image2D directLightingImage;
layout (set = 1, binding = 2, rgba32f) uniform image2D sceneFeatures1Image;
layout (set = 1, binding = 3, rgba32f) uniform image2D sceneFeatures2Image;
layout (set = 1, binding = 4) uniform accelerationStructureNV topLevelAccelerationStructure;

//In parameters.
layout(location = 0) rayPayloadNV PrimaryRayPayload rayPayload;

void main() 
{
	//Inidicate that this is the top recursion level.
	rayPayload.currentRecursionDepth = 0;

	//Generate the random vector.
	rayPayload.randomVector = normalize(vec3(	RandomFloat(vec3(gl_LaunchIDNV.xy, globalRandomSeed1)) * 2.0f - 1.0f,
												RandomFloat(vec3(gl_LaunchIDNV.xy, globalRandomSeed2)) * 2.0f - 1.0f,
												RandomFloat(vec3(gl_LaunchIDNV.xy, globalRandomSeed3)) * 2.0f - 1.0f));

	traceNV(
			topLevelAccelerationStructure, 																//topLevel
			gl_RayFlagsOpaqueNV, 																		//rayFlags
			0xff, 																						//cullMask
			0, 																							//sbtRecordOffset
			0, 																							//sbtRecordStride
			0, 																							//missIndex
			perceiverWorldPosition, 																	//origin
			CATALYST_RAY_TRACING_T_MINIMUM, 															//Tmin
			CalculateRayDirection((vec2(gl_LaunchIDNV.xy) + vec2(0.5f)) / vec2(gl_LaunchSizeNV.xy)), 	//direction
			CATALYST_RAY_TRACING_T_MAXIMUM, 															//Tmax
			0 																							//payload
			);

	imageStore(indirectLightingImage, ivec2(gl_LaunchIDNV.xy), vec4(ToneMap(rayPayload.indirectLighting), 1.0f));
    imageStore(directLightingImage, ivec2(gl_LaunchIDNV.xy), vec4(ToneMap(rayPayload.directLighting), 1.0f));
    imageStore(sceneFeatures1Image, ivec2(gl_LaunchIDNV.xy), vec4(rayPayload.normal, rayPayload.depth));
    imageStore(sceneFeatures2Image, ivec2(gl_LaunchIDNV.xy), vec4(rayPayload.roughness, rayPayload.metallic, rayPayload.ambientOcclusion, 1.0f));
}
