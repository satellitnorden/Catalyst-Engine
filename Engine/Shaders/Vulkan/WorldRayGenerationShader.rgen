//Version declaration.
#version 460

//Extensions.
#extension GL_GOOGLE_include_directive : enable

//Includes.
#include "CatalystShaderCommon.glsl"
#include "CatalystRayTracingCore.glsl"

layout (set = 1, binding = 0, rgba32f) uniform image2D image;
layout (set = 1, binding = 1) uniform accelerationStructureNV topLevelAccelerationStructure;

layout(location = 0) rayPayloadNV vec4 rayPayload;

/*
*	Calculates the direction.
*/
vec3 CalculateDirection()
{
	vec2 center = vec2(gl_LaunchIDNV.xy) + vec2(0.5f);
    vec2 textureCoordinate = center / vec2(gl_LaunchSizeNV.xy);
    vec2 nearPlaneCoordinate = textureCoordinate * 2.0f - 1.0f;
	vec4 viewSpacePosition = inverseProjectionMatrix * vec4(vec3(nearPlaneCoordinate, 1.0f), 1.0f);
	float inverseViewSpacePositionDenominator = 1.0f / viewSpacePosition.w;
	viewSpacePosition *= inverseViewSpacePositionDenominator;
	vec4 worldSpacePosition = inversePerceiverMatrix * viewSpacePosition;

	vec3 worldPosition = vec3(worldSpacePosition.x, worldSpacePosition.y, worldSpacePosition.z);

	vec3 randomPosition = normalize(vec3(	RandomFloat(vec3(gl_LaunchIDNV.xy, globalRandomSeed * EULERS_NUMBER)),
											RandomFloat(vec3(gl_LaunchIDNV.xy, globalRandomSeed * PHI)),
											RandomFloat(vec3(gl_LaunchIDNV.xy, globalRandomSeed * PI)))) * 0.0000625f; //0.0000025f step

	return normalize(worldPosition + randomPosition - perceiverWorldPosition);
}

void main() 
{
	//Inidicate that this is the top recursion level.
	rayPayload.w = float(0.0f);

	traceNV(
			topLevelAccelerationStructure, 	//topLevel
			gl_RayFlagsOpaqueNV, 			//rayFlags
			0xff, 							//cullMask
			0, 								//sbtRecordOffset
			0, 								//sbtRecordStride
			0, 								//missIndex
			perceiverWorldPosition, 		//origin
			0.1f, 							//Tmin
			CalculateDirection(), 			//direction
			CATALYST_RAY_TRACING_T_MAX, 	//Tmax
			0 								//payload
			);

    imageStore(image, ivec2(gl_LaunchIDNV.xy), vec4(rayPayload.rgb, 1.0));
}
