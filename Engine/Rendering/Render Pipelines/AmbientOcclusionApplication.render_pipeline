//Declare uniform buffer includes.
IncludeUniformBuffer(Camera);
IncludeUniformBuffer(General);

//Declare compute render targets.
ComputeRenderTarget(AmbientOcclusion, R_UINT8);
ComputeRenderTarget(SceneFeatures2, RGBA_FLOAT32);
ComputeRenderTarget(SceneFeatures3, RGBA_UINT8);
ComputeRenderTarget(SceneFeatures2Half, RGBA_FLOAT32);

//Declare shader function library includes.
IncludeShaderFunctionLibrary(Camera);
IncludeShaderFunctionLibrary(Noise);

//Set the load/store operators.
ColorLoadOperator(LOAD);
ColorStoreOperator(STORE);

//Subscribe to input streams.
SubscribeToInputStream(ComputeEighth);

//The compute shader.
Compute
{
    //Declare the compute local size.
    ComputeLocalSize(8, 8, 1);

    //Cache the compute dimensions.
    uvec3 compute_dimensions = ComputeDimensions();

    //Calculate the screen coordinate.
    vec2 screen_coordinate = (vec2(COMPUTE_GLOBAL_ID.xy) + vec2(0.5f)) / vec2(compute_dimensions);

    //Calculate the depth of the current pixel.
    float depth = LinearizeDepth(ImageLoad(SceneFeatures2, ivec2(COMPUTE_GLOBAL_ID.xy)).w);

    //Retrieve the ambient occlusion.
    float ambient_occlusion;

    {
        ivec2 sample_coordinate_1 = ivec2(COMPUTE_GLOBAL_ID.xy) / 2;
        ivec2 sample_coordinate_2 = min(ivec2(COMPUTE_GLOBAL_ID.xy) / 2 + ivec2(0, 1), ivec2(HALF_MAIN_RESOLUTION));
        ivec2 sample_coordinate_3 = min(ivec2(COMPUTE_GLOBAL_ID.xy) / 2 + ivec2(1, 0), ivec2(HALF_MAIN_RESOLUTION));
        ivec2 sample_coordinate_4 = min(ivec2(COMPUTE_GLOBAL_ID.xy) / 2 + ivec2(1, 1), ivec2(HALF_MAIN_RESOLUTION));
        
        float ambient_occlusion_1 = ImageLoad(AmbientOcclusion, sample_coordinate_1).x;
        float ambient_occlusion_2 = ImageLoad(AmbientOcclusion, sample_coordinate_2).x;
        float ambient_occlusion_3 = ImageLoad(AmbientOcclusion, sample_coordinate_3).x;
        float ambient_occlusion_4 = ImageLoad(AmbientOcclusion, sample_coordinate_4).x;
        
        float depth_1 = LinearizeDepth(ImageLoad(SceneFeatures2Half, sample_coordinate_1).w);
        float depth_2 = LinearizeDepth(ImageLoad(SceneFeatures2Half, sample_coordinate_2).w);
        float depth_3 = LinearizeDepth(ImageLoad(SceneFeatures2Half, sample_coordinate_3).w);
        float depth_4 = LinearizeDepth(ImageLoad(SceneFeatures2Half, sample_coordinate_4).w);

         float horizontal_weight = fract(screen_coordinate.x * HALF_MAIN_RESOLUTION.x);
        float vertical_weight = fract(screen_coordinate.y * HALF_MAIN_RESOLUTION.y);

        float weight_1 = (1.0f - horizontal_weight) * (1.0f - vertical_weight);
	    float weight_2 = (1.0f - horizontal_weight) * vertical_weight;
	    float weight_3 = horizontal_weight * (1.0f - vertical_weight);
	    float weight_4 = horizontal_weight * vertical_weight;

        weight_1 = max(weight_1 * exp(-abs(depth - depth_1)), FLOAT32_EPSILON);
        weight_2 = max(weight_2 * exp(-abs(depth - depth_2)), FLOAT32_EPSILON);
        weight_3 = max(weight_3 * exp(-abs(depth - depth_3)), FLOAT32_EPSILON);
        weight_4 = max(weight_4 * exp(-abs(depth - depth_4)), FLOAT32_EPSILON);

        float total_weight_reciprocal = 1.0f / (weight_1 + weight_2 + weight_3 + weight_4);

	    weight_1 *= total_weight_reciprocal;
	    weight_2 *= total_weight_reciprocal;
	    weight_3 *= total_weight_reciprocal;
        weight_4 *= total_weight_reciprocal;

        ambient_occlusion = ambient_occlusion_1 * weight_1
                            + ambient_occlusion_2 * weight_2
                            + ambient_occlusion_3 * weight_3
                            + ambient_occlusion_4 * weight_4;
   }

    //Bias the ambient occlusion.
    ambient_occlusion = (ambient_occlusion * ambient_occlusion * ambient_occlusion * ambient_occlusion);

    //Add some noise to the ambient occlusion so that the temporal anti aliasing pass has an easier time cleaning it up.
    ambient_occlusion *= mix(0.875f, 1.125f, InterleavedGradientNoise(uvec2(COMPUTE_GLOBAL_ID.xy), FRAME));

    //Load the material properties.
    vec4 material_properties = ImageLoad(SceneFeatures3, ivec2(COMPUTE_GLOBAL_ID.xy));

    //Modify the ambient occlusion term.
    material_properties.z *= ambient_occlusion;

    //Store the modified material properties.
    ImageStore(SceneFeatures3, ivec2(COMPUTE_GLOBAL_ID.xy), material_properties);
}