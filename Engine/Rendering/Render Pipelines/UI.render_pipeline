//Declare storage buffer includes.
IncludeStorageBuffer(UI);

//Declare output render targets.
OutputRenderTarget(SceneLowDynamicRange1);

//Declare render resolution.
RenderResolution(MAIN_FULL);

//Set the cull mode.
CullMode(NONE);

//Set depth/stencil properties.
DepthCompareOperator(GREATER_OR_EQUAL);

//Set the load/store operators.
ColorLoadOperator(LOAD);
ColorStoreOperator(STORE);
DepthStencilLoadOperator(LOAD);
DepthStencilStoreOperator(STORE);

//Set the topology.
Topology(TRIANGLE_FAN);

//Set blend properties.
BlendEnable();
BlendColorSourceFactor(SOURCE_ALPHA);
BlendColorDestinationFactor(ONE_MINUS_SOURCE_ALPHA);
BlendColorOperator(ADD);
BlendAlphaSourceFactor(ONE);
BlendAlphaDestinationFactor(ZERO);
BlendAlphaOperator(ADD);

//Declare samplers.
Sampler(SAMPLER /* Name */, LINEAR /* Magnification Filter */, LINEAR /* Mipmap Mode */, CLAMP_TO_EDGE /* Address Mode */, 0 /* Anisotropic Samples */);

//The vertex shader.
Vertex
{
    //Declare output parameters.
    OutputParameter(vec2, OutTextureCoordinate);
    OutputParameter(uint, OutInstanceIndex);

    //Retrieve the position and texture coordinate.
    vec4 position = RENDER_COMMANDS[INSTANCE_INDEX]._Positions[VERTEX_INDEX];
    vec2 texture_coordinate = RENDER_COMMANDS[INSTANCE_INDEX]._TextureCoordinates[VERTEX_INDEX];

    //Pass data to the fragment shader.
    OutTextureCoordinate = texture_coordinate;
    OutInstanceIndex = INSTANCE_INDEX;

    //Output the position.
    OutputVertexPosition(position);
}

//The fragment shader.
Fragment
{
    //Declare input parameters.
    InputParameter(vec2, InTextureCoordinate);
    InputParameter(uint, InInstanceIndex);

    //Calculate the color.
    vec4 color;

    switch (RENDER_COMMANDS[InInstanceIndex]._Mode)
    {
        case RENDER_COMMAND_MODE_COLOR:
        {
            color = UnpackColor(RENDER_COMMANDS[InInstanceIndex]._ColorOrTexture);

            break;
        }

        case RENDER_COMMAND_MODE_TEXT:
        {
            float text_sdf = texture(sampler2D(TEXTURES[RENDER_COMMANDS[InInstanceIndex]._ColorOrTexture], SAMPLER), InTextureCoordinate).x;
            float opacity = smoothstep(0.5f - RENDER_COMMANDS[InInstanceIndex]._Parameter1, 0.5f, text_sdf);
            color = vec4(1.0f, 1.0f, 1.0f, opacity);

            break;
        }
    }

    //Unpack the color/opacity.
    vec4 color_opacity = UnpackColor(RENDER_COMMANDS[InInstanceIndex]._ColorOpacity);

    //Apply the color/opacity.
    color *= color_opacity;

    //Output the fragments.
    OutputFragment(SceneLowDynamicRange1, color);
}