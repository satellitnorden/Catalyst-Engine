//Declare uniform buffer includes.
IncludeUniformBuffer(Camera);
IncludeUniformBuffer(General);

//Declare shader function library includes.
IncludeShaderFunctionLibrary(Camera);
IncludeShaderFunctionLibrary(Temporal);

//Declare input render targets.
InputRenderTarget(SceneFeatures2Half /* Identifier */, NEAREST /* Magnification Filter */, NEAREST /* Mipmap Mode */, CLAMP_TO_EDGE /* Address Mode */);
InputRenderTarget(SceneFeatures4Half /* Identifier */, NEAREST /* Magnification Filter */, NEAREST /* Mipmap Mode */, CLAMP_TO_EDGE /* Address Mode */);
InputRenderTarget(VolumetricLightingNearest /* Identifier */, NEAREST /* Magnification Filter */, NEAREST /* Mipmap Mode */, CLAMP_TO_EDGE /* Address Mode */);
InputRenderTarget(VolumetricLightingLinear /* Identifier */, LINEAR /* Magnification Filter */, NEAREST /* Mipmap Mode */, CLAMP_TO_EDGE /* Address Mode */);
InputRenderTarget(PreviousTemporalBuffer /* Identifier */, LINEAR /* Magnification Filter */, NEAREST /* Mipmap Mode */, CLAMP_TO_EDGE /* Address Mode */);

//Declare output render targets.
OutputRenderTarget(CurrentTemporalBuffer);
OutputRenderTarget(CurrentVolumetricLighting);

//Declare render resolution.
RenderResolution(MAIN_HALF);

//Set the load/store operators.
ColorStoreOperator(STORE);

//Set the topology.
Topology(TRIANGLE_FAN);

//Subscribe to input streams.
SubscribeToInputStream(Viewport);

//The vertex shader.
IncludeCommonVertexShader(ViewportScreenCoordinate);

//The fragment shader.
Fragment
{
    //Declare input parameters.
    InputParameter(vec2, InScreenCoordinate);

	//Constants.
	#define FEEDBACK_FACTOR (0.9f)

    //Calculate the minimum/maximum color values in the neighborhood of the current frame. Also find the velocity of the closest fragment.
	float closest_depth = 0.0f;
	vec2 closest_velocity = vec2(0.0f);
	vec3 minimum = vec3(FLOAT32_MAXIMUM);
	vec4 center = vec4(0.0f);
	vec3 maximum = vec3(0.0f);

	for (int Y = -3; Y <= 3; ++Y)
	{
		for (int X = -3; X <= 3; ++X)
		{
			vec2 sample_coordinate = InScreenCoordinate + vec2(float(X), float(Y)) * INVERSE_FULL_MAIN_RESOLUTION;
			
			vec4 neighborhood_sample = texture(VolumetricLightingNearest, sample_coordinate);

			minimum = min(minimum, neighborhood_sample.rgb);
			center += neighborhood_sample * float(X == 0 && Y == 0);
			maximum = max(maximum, neighborhood_sample.rgb);

			float neighborhood_depth = texture(SceneFeatures2Half, sample_coordinate).w;

			if (closest_depth < neighborhood_depth)
			{
				closest_depth = neighborhood_depth;
				closest_velocity = texture(SceneFeatures4Half, sample_coordinate).xy;
			}
		}
	}

	//Calculate the previous screen coordinate.
	vec2 previous_screen_coordinate = InScreenCoordinate - closest_velocity;

	//Sample the previous frame texture.
	vec4 previous_frame = texture(PreviousTemporalBuffer, previous_screen_coordinate);

	//Constraint the previous frame.
	previous_frame.rgb = Constrain(previous_frame.rgb, minimum, maximum);

	/*
	*	Calculate the weight between the current frame and the history depending on certain criteria.
	*
	*	1. Is the sample coordinate valid?
	*/
	float previous_frame_weight = 1.0f;

	previous_frame_weight *= float(ValidScreenCoordinate(previous_screen_coordinate));

	//Blend the frames.
	vec4 blended_frame_1 = mix(texture(VolumetricLightingLinear, InScreenCoordinate - CURRENT_FRAME_JITTER), previous_frame, previous_frame_weight * FEEDBACK_FACTOR);
	vec4 blended_frame_2 = mix(center, previous_frame, previous_frame_weight * FEEDBACK_FACTOR);

    //Output the fragments.
    OutputFragment(CurrentTemporalBuffer, blended_frame_1);
    OutputFragment(CurrentVolumetricLighting, blended_frame_2);
}