/*
*	Calculates wind displacement.
*	Requires the Wind uniform buffer to be bound.
*/
vec3 CalculateWindDisplacement(vec3 world_position, vec3 vertex_position, vec3 normal, vec4 wind_direction_speed, float wind_time)
{
	#define NUMBER_OF_OCTAVES (4)

	//Calculate the wind influence at this point.
	float wind_influence = 0.0f;

	{
		float amplitude = 1.0f;
		float frequency = 1.0f;
		float vertex_influence = 0.0f;

		for (uint i = 0; i < NUMBER_OF_OCTAVES; ++i)
		{
			vec3 direction = RotateYaw(wind_direction_speed.xyz, float(i) / float(NUMBER_OF_OCTAVES) * TAU);

			float world_dot = dot(world_position, direction);
			float vertex_dot = dot(vertex_position, direction);

			wind_influence += (sin((world_dot + (vertex_dot * vertex_influence) + wind_time) * wind_direction_speed.w * frequency) * 0.5f + 0.5f) * amplitude;

			amplitude *= 0.5f;
			frequency *= 2.0f;
			vertex_influence += 0.25f;
		}
	}

	//Calculate the displacement.
	vec3 displacement;
	displacement.xz = wind_direction_speed.xz * wind_direction_speed.w * wind_influence;
	displacement.y = -wind_influence * 0.5f;

	//Modify the displacement so it doesn't affect the bottom of the mesh.
	displacement *= max(vertex_position.y * 0.25f, 0.0f);

	//Return the displacement.
	return displacement;
}

/*
*	Calculates previous wind displacement.
*	Requires the Wind uniform buffer to be bound.
*/
vec3 CalculatePreviousWindDisplacement(vec3 world_position, vec3 vertex_position, vec3 normal)
{
	return CalculateWindDisplacement(world_position, vertex_position, normal, PREVIOUS_WIND_DIRECTION_SPEED, PREVIOUS_WIND_TIME);
}

/*
*	Calculates current wind displacement.
*	Requires the Wind uniform buffer to be bound.
*/
vec3 CalculateCurrentWindDisplacement(vec3 world_position, vec3 vertex_position, vec3 normal)
{
	return CalculateWindDisplacement(world_position, vertex_position, normal, CURRENT_WIND_DIRECTION_SPEED, CURRENT_WIND_TIME);
}