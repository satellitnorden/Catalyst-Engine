//Constants.
#define TERRAIN_MINIMUM_DISPLACEMENT (0.0001f)
#define BIAS_DISPLACEMENT(X) (pow(X, 16.0f))
#define TERRAIN_MATERIAL_SCALE (0.5f)

/*
*   Terrain material struct definition.
*/
struct TerrainMaterial
{
    vec3 albedo;
    vec4 normal_map_displacement;
    vec4 material_properties;
};

/*
*   Calculates the material coordinate for the given world position.
*/
vec2 CalculateTerrainMaterialCoordinate(vec3 world_position, vec2 tile_index, vec3 normal)
{
#if 1
    //Take the absolute of the normal.
    vec3 absolute_normal = abs(normal);

    //Calculate the tile.
    bool x_dominant = absolute_normal.x > absolute_normal.y && absolute_normal.x > absolute_normal.z;
    bool z_dominant = absolute_normal.z > absolute_normal.x && absolute_normal.z > absolute_normal.y;
    vec2 tile = world_position.yz * float(x_dominant) + world_position.xy * float(z_dominant) + world_position.xz * float(!x_dominant && !z_dominant);

    tile *= TERRAIN_MATERIAL_SCALE;

    //Calculate the random rotation.
    float random_rotation = mix(-PI, PI, RandomFloat(tile_index));

    //Randomly rotate the tile.
    tile -= tile_index;
    tile -= 0.5f;
    tile = RotateYaw(vec3(tile.x, 0.0f, tile.y), random_rotation).xz;
    tile += 0.5f;
    tile += tile_index;

    //Return the tile!
    return tile;
#else
    return vec2(world_position.x, world_position.z) * 0.5f;
#endif
}

/*
*   Returns the highest index.
*/
uint HighestIndex(float value_1, float value_2, float value_3, float value_4)
{
    uint highest_index = 0;
    float highest_value = value_1;

    if (highest_value < value_2)
    {
        highest_index = 1;
        highest_value = value_2;
    }

    if (highest_value < value_3)
    {
        highest_index = 2;
        highest_value = value_3;
    }

    if (highest_value < value_4)
    {
        highest_index = 3;
        highest_value = value_4;
    }

    return highest_index;
}